TITLE BRT

RELOCATABLE

PDLEN=400
PDLEN.=100

A==1
B==2
C==3
D==4
E==5
;14 AND UP USED BY B

START:	.BREAK 12,[..RJCL,,JCL]
	MOVE A,[440700,,JCL-1]	;ZERO TERMINATE JCL
START1:	ILDB B,A
	CAIN B,3
	JRST START2
	CAIN B,15
	JRST START2
	CAIE B,37
	JRST START1
START2:	MOVEI B,0
	DPB B,A
	; INIT IO
	SETZ 0,
	.OPEN 0,[.UAI,,'TTY ? SIXBIT /B/ ? SIXBIT /INPUT/]
	.LOSE
	MOVEI A,.UAI
	HRRZM A,IOTAB+0
	.OPEN 1,[.UAO,,'TTY ? SIXBIT /B/ ? SIXBIT /OUTPUT/]
	.LOSE
	MOVEI A,.UAO
	HRRZM A,IOTAB+1
	JRST STARTB

.INSRT B; BI >
DEFINE OP A
	JSP B"IP,B"A
TERMIN
DEFINE OP2 A,X
	JSP B"IP,B"A ? X
TERMIN

STARTB:	MOVE B"P,[-PDLEN.,,PDL.-1]
	MOVE B"DP,[-PDLEN,,PDL-1]
	OP2 S,2
	OP2 IX,%"MAIN"
	OP N1
	OP2 IX,%"EXIT
	OP N1
	.VALUE

%"ARGS": .+1
JCL:	BLOCK 20.

%"FOUT":	1
%"FIN":	0
XOPEN:	.OPEN OPENF
XCLOSE:	.CLOSE
XIOT:	.IOT A

OPENF:	0,,'DSK
	SIXBIT /FOO   /
	SIXBIT /BAR   /

IOTAB:	REPEAT 20, -1,,0

%"IOTAB": IOTAB

;SKIP IF CH CONTAINS A VALID CHANNEL NUMBER
DEFINE VALCHN,CH
	SKIPL CH
	CAIL CH,20
TERMIN

%"OPENR":	.+2
%"OPENW":	.+2
	SKIPA C,[.UAI]
	MOVEI C,.UAO
	HRLM C,OPENF
	MOVE E,2(B"SP)
	VALCHN E
	JSP D,FINDCH
	MOVE B,3(B"SP)
	MOVEM B,OPENF+1
	MOVE B,4(B"SP)
	MOVEM B,OPENF+2
	DPB E,[270400,,XOPEN]
	XCT XOPEN
	JRST RETERR
	HRRZM C,IOTAB(E)
	HRRZ A,E
	JRST RETA
;RETURN UNUSED CHANNEL IN RT(E), OR ERROR OUT
FINDCH:	MOVE E,[-20,,0]
	SKIPGE IOTAB(E)
	JRST (D)
	AOBJN E,.-2
	JRST RETERR

%"CLOSE":	.+1
	MOVE E,2(B"SP)
	VALCHN E
	JRST RETERR
	DPB E,[270400,,XCLOSE]
	XCT XCLOSE
	HRROM 0,IOTAB(E)
	JRST RETA

%"PUTCHAR":	.+1
	MOVE E,%"FOUT
	VALCHN E
	JRST RETERR
	MOVE A,[.UAO]
	CAME A,IOTAB(E)
	JRST RETERR
	DPB E,[270400,,XIOT]
	MOVE E,XIOT
	MOVE D,2(B"SP)
	MOVE B,[430700,,D]
	MOVEI C,5
PUTCH1:	ILDB A,B
	JUMPE A,PUTCH2
	CAIE A,12
	JRST PUTCH2-1
	MOVEI A,15
	XCT E
	MOVEI A,12
	XCT E
PUTCH2:	SOJG C,PUTCH1
	JRST B"N11

%"GETCHAR":	.+1
	MOVE E,%"FIN
	VALCHN E
	JRST RETERR
	MOVE A,[.UAI]
	CAME A,IOTAB(E)
	JRST RETERR
	DPB E,[270400,,XIOT]
	MOVE E,XIOT
GETCH1:	XCT E
	CAIN A,15	;IGNORE CR
	JRST GETCH1
	CAIN A,3	;^C IS EOF
	HRRO A,A	;WEIRD
RETA:	ADD B"SP,[1,,1]
	PUSH B"SP,A
	JRST B"N7

RETERR:	SETO A,
	JRST RETA

%"EXIT":	.+1
	; CLOSING ALL CHANNELS NOT NECESSARY
	; IF WE KILL THE JOB, BUT MAYBE WE WANT TO KEEP IT AROUND?
	MOVEI A,17
EXIT1:	DPB A,[270400,,XCLOSE]
	XCT XCLOSE
	HRROM 0,IOTAB(A)
	SOJGE A,EXIT1
	.VALUE [ASCIZ /:KILL /]
;	.VALUE

%"VALUE":	.+1
	MOVE A,2(B"SP)
	.VALUE (A)
	JRST B"N7

%"RSNAME":	.+1
	.SUSET [.RSNAM,,A]
	JRST RETA

CPTAB:	350700,,0
	260700,,0
	170700,,0
	100700,,0
	010700,,0
CPTR:	MOVE B,3(B"SP)
	IDIVI B,5
	ADD B,2(B"SP)
	HLL B,CPTAB(C)
	JRST (D)
%"CHAR":	.+1
	JSP D,CPTR
	LDB A,B
	JRST RETA
%"LCHAR":	.+1
	JSP D,CPTR
	MOVE A,4(B"SP)
	DPB A,B
	JRST RETA

PDL:	BLOCK PDLEN
PDL.:	BLOCK PDLEN.


END START
